
task copyDocs(type: Copy, dependsOn: rootProject.getTasksByName("dokka", true)) {
    from (rootProject.getTasksByName("dokka", true).collect { "$it.project.buildDir/dokka" }) {
        include "**/*.md"
    }
    from "docs"
    into "$buildDir/gh-pages"
}

task copyExampleFrontendJs(type: Copy, dependsOn: ':example-frontend-js:bundle') {
    def srcBuildDir = project(':example-frontend-js').buildDir
    from "$srcBuildDir/dist"
    into "$buildDir/gh-pages/example-frontend-js"
}

task site(type: Exec, description: 'Generate github pages', dependsOn: [copyDocs, copyExampleFrontendJs]) {
    workingDir "$buildDir/gh-pages"
    commandLine 'jekyll', 'build'
}

task clean(type: Delete) {
    delete buildDir
}

