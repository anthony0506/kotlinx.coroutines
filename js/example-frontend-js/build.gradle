/*
 * Copyright 2016-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

project.kotlin {
    js(LEGACY) {
        binaries.executable()
        browser {
            distribution {
                directory = new File(directory.parentFile, "dist")
            }
            webpackTask {
                cssSupport.enabled = true
            }
            runTask {
                cssSupport.enabled = true
            }
            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }

    sourceSets {
        main.dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-html-js:$html_version"
            implementation(npm("html-webpack-plugin", "3.2.0"))
        }
    }
}


// Workaround for bug in 1.4.20 that will be fixed in 1.4.21, KT-43668
tasks.withType(org.jetbrains.kotlin.gradle.targets.js.npm.tasks.KotlinPackageJsonTask) {
    doFirst {
        def producerField = it.class.superclass.getDeclaredMethod("getProducer")
        producerField.setAccessible(true);
        def producer = producerField.invoke(it)
        def items = producer.fileCollectionDependencies
        def list2 = new HashSet<FileCollectionDependency>()
        for (FileCollectionDependency item : items) {
            for (File file : item.files) {
                if (!file.isFile()) {
                    list2.add(item)
                    break
                }
            }
        }
        items.removeAll(list2)
    }
}
